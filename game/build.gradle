apply plugin: 'com.android.library'
apply plugin: 'maven'


android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main {
            jni.srcDirs = []
        }
    }
    productFlavors {
    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:19+'
}


uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = 'com.guidebee'
        pom.artifactId = 'game-engine'
        pom.version = '0.9.9'
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file:///workspace/GuidebeeGameEngine/release")
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/*.html'
    exclude '**/*.fnt'
    exclude '**/*.png'
    exclude '**/*.gif'
    exclude '**/*.rl'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar

}